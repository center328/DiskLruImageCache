buildscript {
    repositories {
        mavenCentral()
        maven {
            url uri("https://oss.sonatype.org/content/repositories/snapshots")
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.10.0'
    }
}



apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'android-library'
apply plugin: 'signing'

group 'it.sephiroth.android.library.disklruimagecache'
version '1.0.2'

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.3"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 19
        versionCode 1
        versionName version
    }
}

task displayInfo() {
    doLast {
        logger.lifecycle "  project name: ${project.name} (root: ${rootProject.name})"
        logger.lifecycle "  project version: ${project.version}"
        logger.lifecycle "  project group: ${project.group}"
    }
}

project.ext.outJar = project.name + '-' + version + '.jar'

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.allSource
}

task clearJar(type: Delete) {
    delete 'build/libs/' + project.ext.outJar
}

task makeJar(type: Copy) {
    from('build/bundles/release/')
    into('build/libs/')
    include('classes.jar')
    rename ('classes.jar', project.ext.outJar)   
}


def generatePom() {
    def pom = pom{
        project {
            name 'disklruimagecache'
            groupId project.group
            artifactId project.name
            version project.version
            description 'Simple file based image cache'
            inceptionYear '2014'
            url 'http://blog.sephiroth.it'

            licenses {
                license {
                    name 'Public Domain'
                    url 'https://github.com/sephiroth74/DiskLruImageCache/blob/master/LICENSE'
                    distribution 'repo'
                }
            }

            scm {
                url                 "https://github.com/sephiroth74/DiskLruImageCache"
                connection          "scm:git:git@github.com:sephiroth74/DiskLruImageCache.git"
                developerConnection "scm:git:git@github.com:sephiroth74/DiskLruImageCache.git"
            }


            developers {
                developer {
                    id 'sephiroth74'
                    name 'Alessandro Crugnola'
                    email 'alessandro.crugnola@gmail.com'
                    url 'http://blog.sephiroth.it'
                    roles {
                        role 'author'
                        role 'developer'
                    }
                }
            }
        }
    }
}

task writePom {
    ext.pomXml = file("pom.xml")
    inputs.file(project.buildFile)
    outputs.file(pomXml)
    doLast {
        generatePom().writeTo(pomXml)
    }
}

artifacts {
    archives sourcesJar
}

signing {
    sign configurations.archives
}


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }
            pom = generatePom()

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword )
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: sonatypeUsername, password: sonatypePassword )
            }
        }
    }
}

clean.dependsOn(clearJar)
build.dependsOn(writePom, makeJar)
uploadArchives.dependsOn(build)
preBuild.dependsOn(displayInfo)

repositories {
    mavenCentral()
    maven {
        url uri("https://oss.sonatype.org/content/repositories/snapshots")
    }
}

dependencies {
    compile 'com.jakewharton:disklrucache:2.0.2'
    compile 'org.apache.commons:commons-io:1.3.2'
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}